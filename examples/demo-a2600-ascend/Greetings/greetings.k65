/*
 * OPTIMIZE:
 * - size: check all nocross for necessity
 * - size, speed: replace a=head_base_hmvalues+137 (etc.) with a=#xxx
 */
 

[
	SCR_COL = 6*16+8,
	GREETS_HEART_HEIGHT = 17
]

var scr_data[78]=0x84 ?, scr_zero ?;
var greets_backcol ?;
var scr_curchar ?, scr_mask=0xd5 ?, scr_fontptr[2]=0xd6 ?, scr_pixelstoscroll=0xd8 ?;
var greets_heart_yPos=0xd9 ?, greets_heart_yPosFix=0xda ?, greets_heart_xPos=0xdb ?, greets_heart_color=0xdc ?;
var greets_heart_frame=0xdd ?;
var greets_seed=0xde ?;
var greets_varend ?;


func greets_sl12 {
}


/* pRNG
 */
func greets_rng {
		a=greets_seed
		==goto greets_doEor
		a<< ==goto greets_noEor <goto greets_noEor
greets_doEor:
		a^0x1d
greets_noEor:
		greets_seed=a
}


/* Fine-position object.
 * a: position (0..159)
 * x: object number
 * Does not do the necessary HMOVE, only prepares HM register.
 */
func greets_pos_object {
	wsync greets_sl12 *2 c+ nocross{a-15}>= RESP0,x=a
	a^7 a<< a<< a<< a<< HMP0,x=a
}


inline show_head_until_hair {
	// 0-25: Until hair    
	x=0 {
		HMM0=HMM1=a=head_base_hmvalues,x
		wsync hmove COLUBK=a=greets_backcol COLUPF=a
		x++

		// Needs 33 cycles+sl5
		COLUP1=a=greets_heart_color
		sl5
		y=0 a=[GREETS_HEART_HEIGHT]
		data {0xc7 0xd9}		// dcp greets_heart_yPos
		>=goto .doDraw
		data {0x2c}				// bit nn nn
.doDraw:
		y=greets_heart_yPos
		a=greets_heart,y
		y=0
		COLUP1=y
		GRP1=a

		COLUPF=y
		x?26
	}!=
}


inline show_head_hair {
	// 26-59: Hair
	y=0 {
		HMM0=HMM1=a=head_base_hmvalues,x
		wsync hmove COLUPF=a=greets_backcol
		GRP0=a=head_hair,y y++
		x++
		greets_sl12 greets_sl12 sl8
		COLUPF=a=0x00
		x?60
	}!=
}


inline show_head_until_brow {
	// 60-88: Until brow
	{
		HMM0=HMM1=a=head_base_hmvalues,x
		wsync hmove COLUPF=a=greets_backcol
		x++
		
		// Needs 33 cycles+sl8
		COLUP1=a=greets_heart_color
		sl8
		y=0 a=[GREETS_HEART_HEIGHT]
		data {0xc7 0xd9}		// dcp greets_heart_yPos
		>=goto .doDraw
		data {0x2c}				// bit nn nn
.doDraw:
		y=greets_heart_yPos
		a=greets_heart,y
		y=0
		COLUP1=y
		GRP1=a
		
		COLUPF=y
		x?89
	}!=
}


inline show_head_brow {
	// 89-90: Brow
	HMM0=HMM1=a=head_base_hmvalues,x
	wsync hmove ENABL=a=[OBJ_ENABLE] COLUPF=a=greets_backcol
	x++ sl6
	HMM0=HMM1=a=head_base_hmvalues,x
	HMBL=a=[HM_RIGHT_1]
	COLUPF=a=0x00
	wsync hmove COLUPF=a=greets_backcol sl12 x++
	HMBL=a=[HM_NO_MOTION] COLUPF=a //=0x00
}


inline show_head_until_scroller {
	// 91-135: Until scroller
	// "nocross" to avoid k65 bank switching bug
	nocross {
		HMM0=HMM1=a=head_base_hmvalues,x
		wsync hmove ENABL=a=[OBJ_DISABLE] COLUPF=a=greets_backcol
		x++

		greets_sl12 greets_sl12 sl9
		COLUPF=a=0
		x?136
	}!=
}


inline show_head_scroller {
	// 136-163: Scroller
	// 136, @48
	// Init player gfx values
	HMP0=a HMP1=a=[HM_LEFT_1]
	// @58
	HMM0=HMM1=a=head_base_hmvalues+136
	// @0
	hmove COLUPF=a=greets_backcol
	VDELP0=VDELP1=a=[VDEL_ENABLE]

	// 137
	// position players
	greets_sl12 sl2
	// @30
	RESP0=a RESP1=a
	COLUPF=a=0x00
	HMM0=HMM1=a=head_base_hmvalues+137
	wsync hmove
	COLUPF=x=greets_backcol
	// @9
	greets_sl12 sl11 HMP1=x=[HM_NO_MOTION]
	COLUPF=x
	// @40
	//greets_sl12 sl7


	// Line 1, hmm 138, @59
	HMM0=HMM1=x=0x10
	GRP0=a=scr_data+0 a=scr_data+78
	wsync
	// @0
	hmove
	COLUP0=COLUP1=x=[SCR_COL] COLUPF=x=greets_backcol
	// @17
	GRP1=a GRP0=a=scr_data+78 sl9
	// @35
	GRP1=a GRP0=a GRP1=a GRP0=a
	// @47
	COLUP0=a=0 COLUP1=a COLUPF+256=a

	// Line 2, hmm 139, @59
	HMM0=HMM1=a *2
	GRP0=a=scr_data+0 a=scr_data+1
	// @0
	hmove
	COLUP0=COLUP1=x=[SCR_COL] COLUPF=x=greets_backcol
	// @17
	GRP1=a GRP0=a=scr_data+78 sl9
	// @35
	GRP1=a GRP0=a GRP1=a GRP0=a
	// @47
	COLUP0=a=0 COLUP1=a COLUPF+256=a

	// Line 3, hmm 140, @59
	//sl8 //HMM0=HMM1=x=0x00
	GRP0=a=scr_data+6 a=scr_data+1
	wsync
	// @0
	hmove
	COLUP0=COLUP1=x=[SCR_COL] COLUPF=x=greets_backcol
	// @17
	GRP1=a GRP0=a=scr_data+2 a=scr_data+78 sl6
	// @35
	GRP1=a GRP0=a GRP1=a GRP0=a
	// @47
	COLUP0=a=0 COLUP1=a COLUPF+256=a

	// Line 4, hmm 141, @59
	HMM0=HMM1=x=0x10
	GRP0=a=scr_data+6 a=scr_data+7
	// @0
	hmove
	COLUP0=COLUP1=x=[SCR_COL] COLUPF=x=greets_backcol
	// @17
	GRP1=a GRP0=a=scr_data+2 a=scr_data+78 sl6
	// @35
	GRP1=a GRP0=a GRP1=a GRP0=a
	// @47
	COLUP0=a=0 COLUP1=a COLUPF+256=a

	// Line 5, hmm 142, @59
	//sl8 //HMM0=HMM1=x=0x10
	GRP0=a=scr_data+12 a=scr_data+7
	wsync
	// @0
	hmove
	COLUP0=COLUP1=x=[SCR_COL] COLUPF=x=greets_backcol
	// @17
	GRP1=a GRP0=a=scr_data+8 a=scr_data+3 x=scr_data+78 y=scr_data+78
	// @35
	GRP1=a GRP0=x GRP1=y GRP0=a
	// @47
	COLUP0=a=0 COLUP1=a COLUPF+256=a

	// Line 6, hmm 143, @59
	HMM0=HMM1=a sl2
	GRP0=a=scr_data+12 a=scr_data+13
	// @0
	hmove
	COLUP0=COLUP1=x=[SCR_COL] COLUPF=x=greets_backcol
	// @17
	GRP1=a GRP0=a=scr_data+8 a=scr_data+3 x=scr_data+4 y=scr_data+78
	// @35
	GRP1=a GRP0=x GRP1=y GRP0=a
	// @47
	COLUP0=a=0 COLUP1=a COLUPF+256=a

	// Line 7, hmm 144, @59
	//sl8 //HMM0=HMM1=x=0x00
	GRP0=a=scr_data+18 a=scr_data+13
	wsync
	// @0
	hmove
	COLUP0=COLUP1=x=[SCR_COL] COLUPF=x=greets_backcol
	// @17
	GRP1=a GRP0=a=scr_data+14 a=scr_data+9 x=scr_data+4 y=scr_data+5
	// @35
	GRP1=a GRP0=x GRP1=y GRP0=a
	// @47
	COLUP0=a=0 COLUP1=a COLUPF+256=a

	// Line 8, hmm 145, @59
	//sl8 //HMM0=HMM1=x=0x00
	GRP0=a=scr_data+18 a=scr_data+19
	// @0
	wsync
	hmove
	COLUP0=COLUP1=x=[SCR_COL] COLUPF=x=greets_backcol
	// @17
	GRP1=a GRP0=a=scr_data+20 a=scr_data+15 x=scr_data+10 y=scr_data+11
	// @35
	GRP1=a GRP0=x GRP1=y GRP0=a
	// @47
	COLUP0=a=0 COLUP1=a COLUPF+256=a

	// Line 9, hmm 146, @59
	//sl8 //HMM0=HMM1=x=0x00
	GRP0=a=scr_data+24 a=scr_data+19
	wsync
	// @0
	hmove
	COLUP0=COLUP1=x=[SCR_COL] COLUPF=x=greets_backcol
	// @17
	GRP1=a GRP0=a=scr_data+20 a=scr_data+15 x=scr_data+16 y=scr_data+17
	// @35
	GRP1=a GRP0=x GRP1=y GRP0=a
	// @47
	COLUP0=a=0 COLUP1=a COLUPF+256=a

	// Line 10, hmm 147, @59
	//sl8 //HMM0=HMM1=x=0x00
	GRP0=a=scr_data+24 a=scr_data+25
	wsync
	// @0
	hmove
	COLUP0=COLUP1=x=[SCR_COL] COLUPF=x=greets_backcol
	// @17
	GRP1=a GRP0=a=scr_data+26 a=scr_data+21 x=scr_data+22 y=scr_data+23
	// @35
	GRP1=a GRP0=x GRP1=y GRP0=a
	// @47
	COLUP0=a=0 COLUP1=a COLUPF+256=a

	// Line 11, hmm 148, @59
	//sl8 //HMM0=HMM1=x=0x00
	GRP0=a=scr_data+30 a=scr_data+31
	wsync
	// @0
	hmove
	COLUP0=COLUP1=x=[SCR_COL] COLUPF=x=greets_backcol
	// @17
	GRP1=a GRP0=a=scr_data+32 a=scr_data+27 x=scr_data+28 y=scr_data+29
	// @35
	GRP1=a GRP0=x GRP1=y GRP0=a
	// @47
	COLUP0=a=0 COLUP1=a COLUPF+256=a

	// Line 12, hmm 149, @59
	HMM0=HMM1=x=0xf0
	GRP0=a=scr_data+30 a=scr_data+31
	// @0
	hmove
	COLUP0=COLUP1=x=[SCR_COL] COLUPF=x=greets_backcol
	// @17
	GRP1=a GRP0=a=scr_data+32 a=scr_data+33 x=scr_data+34 y=scr_data+35
	// @35
	GRP1=a GRP0=x GRP1=y GRP0=a
	// @47
	COLUP0=a=0 COLUP1=a COLUPF+256=a

	// Line 13, hmm 150, @59
	//sl8 //HMM0=HMM1=x=0xf0
	GRP0=a=scr_data+36 a=scr_data+37
	wsync
	// @0
	hmove
	COLUP0=COLUP1=x=[SCR_COL] COLUPF=x=greets_backcol
	// @17
	GRP1=a GRP0=a=scr_data+38 a=scr_data+33 x=scr_data+34 y=scr_data+41
	// @35
	GRP1=a GRP0=x GRP1=y GRP0=a
	// @47
	COLUP0=a=0 COLUP1=a COLUPF+256=a

	// Line 14, hmm 151, @59
	HMM0=HMM1=a sl2
	GRP0=a=scr_data+36 a=scr_data+37
	// @0
	hmove
	COLUP0=COLUP1=x=[SCR_COL] COLUPF=x=greets_backcol
	// @17
	GRP1=a GRP0=a=scr_data+38 a=scr_data+39 x=scr_data+40 y=scr_data+47
	// @35
	GRP1=a GRP0=x GRP1=y GRP0=a
	// @47
	COLUP0=a=0 COLUP1=a COLUPF+256=a

	// Line 15, hmm 152, @59
	//sl8 //HMM0=HMM1=x=0x00
	GRP0=a=scr_data+42 a=scr_data+43
	wsync
	// @0
	hmove
	COLUP0=COLUP1=x=[SCR_COL] COLUPF=x=greets_backcol
	// @17
	GRP1=a GRP0=a=scr_data+44 a=scr_data+45 x=scr_data+46 y=scr_data+53
	// @35
	GRP1=a GRP0=x GRP1=y GRP0=a
	// @47
	COLUP0=a=0 COLUP1=a COLUPF+256=a

	// Line 16, hmm 153, @59
	HMM0=HMM1=x=0x10
	GRP0=a=scr_data+42 a=scr_data+43
	// @0
	hmove
	COLUP0=COLUP1=x=[SCR_COL] COLUPF=x=greets_backcol
	// @17
	GRP1=a GRP0=a=scr_data+50 a=scr_data+45 x=scr_data+52 y=scr_data+59
	// @35
	GRP1=a GRP0=x GRP1=y GRP0=a
	// @47
	COLUP0=a=0 COLUP1=a COLUPF+256=a

	// Line 17, hmm 154, @59
	HMM0=HMM1=a sl2
	GRP0=a=scr_data+48 a=scr_data+49
	// @0
	hmove
	COLUP0=COLUP1=x=[SCR_COL] COLUPF=x=greets_backcol
	// @17
	GRP1=a GRP0=a=scr_data+50 a=scr_data+51 x=scr_data+58 y=scr_data+65
	// @35
	GRP1=a GRP0=x GRP1=y GRP0=a
	// @47
	COLUP0=a=0 COLUP1=a COLUPF+256=a

	// Line 18, hmm 155, @59
	//sl8 //HMM0=HMM1=x=0x00
	GRP0=a=scr_data+48 a=scr_data+49
	wsync
	// @0
	hmove
	COLUP0=COLUP1=x=[SCR_COL] COLUPF=x=greets_backcol
	// @17
	GRP1=a GRP0=a=scr_data+56 a=scr_data+57 x=scr_data+64 y=scr_data+71
	// @35
	GRP1=a GRP0=x GRP1=y GRP0=a
	// @47
	COLUP0=a=0 COLUP1=a COLUPF+256=a

	// Line 19, hmm 156, @59
	//sl8 //HMM0=HMM1=x=0x00
	GRP0=a=scr_data+54 a=scr_data+55
	wsync
	// @0
	hmove
	COLUP0=COLUP1=x=[SCR_COL] COLUPF=x=greets_backcol
	// @17
	GRP1=a GRP0=a=scr_data+62 a=scr_data+63 x=scr_data+64 y=scr_data+77
	// @35
	GRP1=a GRP0=x GRP1=y GRP0=a
	// @47
	COLUP0=a=0 COLUP1=a COLUPF+256=a

	// Line 20, hmm 157, @59
	HMM0=HMM1=x=0xf0
	GRP0=a=scr_data+54 a=scr_data+61
	// @0
	hmove
	COLUP0=COLUP1=x=[SCR_COL] COLUPF=x=greets_backcol
	// @17
	GRP1=a GRP0=a=scr_data+62 a=scr_data+63 x=scr_data+70 y=scr_data+78
	// @35
	GRP1=a GRP0=x GRP1=y GRP0=a
	// @47
	COLUP0=a=0 COLUP1=a COLUPF+256=a

	// Line 21, hmm 158, @59
	HMM0=HMM1=a sl2
	GRP0=a=scr_data+60 a=scr_data+61
	// @0
	hmove
	COLUP0=COLUP1=x=[SCR_COL] COLUPF=x=greets_backcol
	// @17
	GRP1=a GRP0=a=scr_data+68 a=scr_data+69 x=scr_data+76 y=scr_data+78
	// @35
	GRP1=a GRP0=x GRP1=y GRP0=a
	// @47
	COLUP0=a=0 COLUP1=a COLUPF+256=a

	// Line 22, hmm 159, @59
	HMM0=HMM1=x=0xf0
	GRP0=a=scr_data+60 a=scr_data+67
	// @0
	hmove
	COLUP0=COLUP1=x=[SCR_COL] COLUPF=x=greets_backcol
	// @17
	GRP1=a GRP0=a=scr_data+68 a=scr_data+75 x=scr_data+78 y=scr_data+78
	// @35
	GRP1=a GRP0=x GRP1=y GRP0=a
	// @47
	COLUP0=a=0 COLUP1=a COLUPF+256=a

	// Line 23, hmm 160, @59
	HMM0=HMM1=x=0xd0
	GRP0=a=scr_data+66 a=scr_data+67
	// @0
	hmove
	COLUP0=COLUP1=x=[SCR_COL] COLUPF=x=greets_backcol
	// @17
	GRP1=a GRP0=a=scr_data+74 a=scr_data+78 sl6
	// @35
	GRP1=a GRP0=a GRP1=a GRP0=a
	// @47
	COLUP0=a=0 COLUP1=a COLUPF+256=a

	// Line 24, hmm 161, @59
	HMM0=HMM1=a sl2
	GRP0=a=scr_data+66 a=scr_data+73
	// @0
	hmove
	COLUP0=COLUP1=x=[SCR_COL] COLUPF=x=greets_backcol
	// @17
	GRP1=a GRP0=a=scr_data+78 sl9
	// @35
	GRP1=a GRP0=a GRP1=a GRP0=a
	// @47
	COLUP0=a=0 COLUP1=a COLUPF+256=a

	// Line 25, hmm 162, @59
	HMM0=HMM1=x=0xf0
	GRP0=a=scr_data+72 a=scr_data+78
	// @0
	hmove
	COLUP0=COLUP1=x=[SCR_COL] COLUPF=x=greets_backcol
	// @17
	GRP1=a GRP0=a=scr_data+78 sl9
	// @35
	GRP1=a GRP0=a GRP1=a GRP0=a
	// @47
	COLUP0=a=0 COLUP1=a COLUPF+256=a

	// Line 26, hmm 163, @59
	HMM0=HMM1=a sl2
	GRP0=a=scr_data+72 a=scr_data+78
	// @0
	hmove
	COLUP0=COLUP1=x=[SCR_COL] COLUPF=x=greets_backcol
	// @17
	GRP1=a GRP0=a=scr_data+78 sl9
	// @35
	GRP1=a GRP0=a GRP1=a GRP0=a
	// @47
	COLUP0=a=0 COLUP1=a COLUPF+256=a

}


inline show_head_until_chin {
	// @59
	// 164
	HMM0=HMM1=a=head_base_hmvalues+164
	x=165
	// @71
	wsync hmove
	COLUPF=a=greets_backcol
	GRP0=GRP1=GRP0=a=0
	greets_sl12 COLUPF=a //=0x00
	
	// 165-175: Until chin
	// "nocross" to avoid k65 bank switching bug
	nocross {
		HMM0=HMM1=a=head_base_hmvalues,x
		wsync hmove COLUPF=a=greets_backcol
		greets_sl12 sl11 COLUPF=a=0x00
		x++ x?176
	}!=
}


inline show_head_chin {
	// 176-185: Chin
	// @43
	y=0
	HMM0=HMM1=a=head_base_hmvalues,x
	// @55
	sl4
	// @59
	RESBL=a ENABL=a=[OBJ_ENABLE]
	a=greets_backcol
	wsync hmove COLUPF=a
	PF1=a=head_pf,y CTRLPF=a=head_ball_ctrlpf,y
	*2
	// @22
	x++ y++
	nocross {
		greets_sl12 sl2
		HMM0=HMM1=a=head_base_hmvalues,x
		HMBL=a=head_hm,y		
		CTRLPF=a=head_ball_ctrlpf,y
		COLUPF=a=0x00
		wsync hmove PF1=a=head_pf,y COLUPF=a=greets_backcol
		x++ y++ y?11
	}!=
}


inline show_head_until_hand {
	// Hand, 186-190
	// @23
	HMBL=a=[HM_LEFT_7]
	ENABL=a=[OBJ_DISABLE]
	greets_sl12 greets_sl12
	// @58
	COLUPF=a=0
	nocross {
		HMM0=HMM1=a=head_base_hmvalues,x
		wsync hmove COLUPF=a=greets_backcol PF1=a=0b10000000
		greets_sl12 greets_sl12 sl5 COLUPF=a=0x00 x++ x?191
	}!=
}


inline show_head_hand {
	// Hand, 191-213

	// Line 1, hmm 190
	wsync hmove COLUPF=x=greets_backcol
	GRP0=x=0x00 GRP1=x GRP0=a=0x03 a=0xf8 sl4
	// ?@28
	COLUPF=x sl4
	// @35
	GRP1=a GRP0=x GRP1=x GRP0=a
	// @47
	HMBL=a=0xe0 HMM0=HMM1=x

	// Line 2, hmm 191
	wsync hmove COLUPF=a=greets_backcol
	GRP0=x GRP1=x GRP0=a=0x03 a=0xfc sl6
	// @28
	COLUPF=x sl4
	// @35
	GRP1=a GRP0=x GRP1=x GRP0=a
	// @47
	HMBL=x

	// Line 3, hmm 192
	wsync hmove COLUPF=x=greets_backcol
	GRP0=a=0x38 GRP1=y=0x00 GRP0=a=0x03 a=0xff x=0x80
	// @28
	COLUPF=y sl4
	// @35
	GRP1=a GRP0=x GRP1=y GRP0=a
	// @47
	//HMM0=HMM1=y HMBL=y

	// Line 4, hmm 193
	wsync hmove COLUPF=x=greets_backcol
	GRP0=a=0x7c GRP1=y GRP0=a=0x03 a=0xff sl4
	// @28
	COLUPF=y y=0xf0 sl2
	// @35
	GRP1=a GRP0=a GRP1=y GRP0=a
	// @47
	//HMM0=HMM1=y=0x00 HMBL=y
	y=0

	// Line 5, hmm 194
	wsync hmove COLUPF=x=greets_backcol
	GRP0=a=0xff GRP1=y GRP0=x=0x01 sl6
	// @28
	COLUPF=y y=0xfc sl2
	// @35
	GRP1=a GRP0=a GRP1=y GRP0=a
	// @47
	//HMM0=HMM1=y=0x00 HMBL=y
	y=0

	// Line 6, hmm 195
	wsync hmove COLUPF=x=greets_backcol
	GRP0=a=0xff GRP1=x=0xf0 GRP0=x=0x01 sl4
	// @28
	COLUPF=y y=0xfe sl2
	// @35
	GRP1=a GRP0=a GRP1=y GRP0=a
	// @47
	HMM0=HMM1=x=0xf0 y=0x00

	// Line 7, hmm 196
	wsync hmove COLUPF=x=greets_backcol
	GRP0=a=0xff GRP1=a GRP0=x=0xfb sl6
	// @28
	COLUPF=y y=0xfe sl2
	// @35
	GRP1=a GRP0=a GRP1=y GRP0=a
	// @47
	HMM0=HMM1=y=0x00

	ENABL=a //[OBJ_ENABLE]

	// Line 8, hmm 197
	wsync hmove COLUPF=x=greets_backcol
	GRP0=a=0x7f GRP1=a=0xff GRP0=a sl6
	// @28
	COLUPF=y sl4
	// @35
	GRP1=a GRP0=a GRP1=a GRP0=a
	// @47
	HMBL=x=0xf0

	// Line 9, hmm 198
	wsync hmove COLUPF=x=greets_backcol
	GRP0=a=0x3f GRP1=a=0xff GRP0=a sl6
	// @28
	COLUPF=y sl4
	// @35
	GRP1=a GRP0=a GRP1=a GRP0=a
	// @47
	//HMM0=HMM1=y HMBL=x=0xf0

	// Line 10, hmm 199
	wsync hmove COLUPF=x=greets_backcol
	GRP0=a=0x0f GRP1=a=0xff GRP0=a sl6
	// @28
	COLUPF=y sl4
	// @35
	GRP1=a GRP0=a GRP1=a GRP0=a
	// @47
	//HMM0=HMM1=y HMBL=x=0xf0

	// Line 11, hmm 200
	wsync hmove COLUPF=x=greets_backcol
	GRP0=a=0x07 GRP1=a=0xff GRP0=a sl6
	// @28
	COLUPF=y sl4
	// @35
	GRP1=a GRP0=a GRP1=a GRP0=a
	// @47
	HMBL=y

	// Line 12, hmm 201
	wsync hmove COLUPF=x=greets_backcol
	GRP0=a=0x01 GRP1=a=0xff GRP0=a sl6
	// @28
	COLUPF=y sl4
	// @35
	GRP1=a GRP0=a GRP1=a GRP0=a
	// @47
	HMBL=x=0xf0

	// Line 13, hmm 202
	wsync hmove COLUPF=x=greets_backcol
	GRP0=y GRP1=a=0x3f GRP0=a=0xff sl6
	// @28
	COLUPF=y sl4
	// @35
	GRP1=a GRP0=a GRP1=a GRP0=a
	// @47
	HMBL=y

	// Line 14, hmm 203
	wsync hmove COLUPF=x=greets_backcol
	GRP0=y GRP1=a=0x0f GRP0=a=0xff sl6
	// @28
	COLUPF=y sl4
	// @35
	GRP1=a GRP0=a GRP1=a GRP0=a
	// @47
	HMM0=HMM1=x=0xf0 HMBL=x

	// Line 15, hmm 204
	wsync hmove COLUPF=x=greets_backcol
	GRP0=y GRP1=y GRP0=a=0x7f a=0xff sl6
	// @28
	COLUPF=y sl4
	// @35
	GRP1=a GRP0=a GRP1=a GRP0=a
	// @47
	HMM0=HMM1=y HMBL=y

	// Line 16, hmm 205
	wsync hmove COLUPF=x=greets_backcol
	GRP0=y GRP1=y GRP0=a=0x03 a=0xff sl6
	// @28
	COLUPF=y sl4
	// @35
	GRP1=a GRP0=a GRP1=a GRP0=a
	// @47
	HMBL=x=0xf0

	// Line 17, hmm 206
	wsync hmove COLUPF=x=greets_backcol
	GRP0=y GRP1=y GRP0=y a=0x3f x=0xff sl6
	// @28
	COLUPF=y sl4
	// @35
	GRP1=a GRP0=x GRP1=x GRP0=a
	// @47
	HMBL=y

	// Line 18, hmm 207
	wsync hmove COLUPF=x=greets_backcol
	GRP0=y GRP1=y GRP0=y a=0x07 x=0xff sl6
	// @28
	COLUPF=y y=0xff sl2
	// @35
	GRP1=a GRP0=x GRP1=y GRP0=a
	// @47
	a=0x00 HMBL=x=0xf0

	// Line 19, hmm 208
	wsync hmove COLUPF=x=greets_backcol
	GRP0=a GRP1=a GRP0=a x=0xff sl8
	// @28
	COLUPF=a y=0xff sl2
	// @35
	GRP1=a GRP0=x GRP1=y GRP0=a
	// @47
	HMBL=a

	// Line 20, hmm 209
	wsync hmove COLUPF=x=greets_backcol
	GRP0=a GRP1=a GRP0=a x=0x07 sl8
	// @28
	COLUPF=a y=0xff sl2
	// @35
	GRP1=a GRP0=x GRP1=y GRP0=a
	// @47
	//HMM0=HMM1=a HMBL=a

	// Line 21, hmm 210
	wsync hmove COLUPF=x=greets_backcol
	GRP0=a GRP1=a GRP0=a x=0x01 sl8
	// @28
	COLUPF=a y=0xff sl2
	// @35
	GRP1=a GRP0=x GRP1=y GRP0=a
	// @47
	HMBL=x=0xf0

	// Line 22, hmm 211
	wsync hmove COLUPF=x=greets_backcol
	GRP0=a GRP1=a GRP0=a sl10
	// @28
	COLUPF=a y=0x7f sl2
	// @35
	GRP1=a GRP0=a GRP1=y GRP0=a
	// @47
	HMBL=a

	// Line 23, hmm 212
	wsync hmove COLUPF=x=greets_backcol
	GRP0=a GRP1=a GRP0=a sl10
	// @28
	COLUPF=a y=0x3f sl2
	// @35
	GRP1=a GRP0=a GRP1=y GRP0=a
	// @47
	//HMM0=HMM1=a HMBL=a


	GRP0=GRP1=GRP0=a
	wsync
	COLUBK=a //=0
}


/* Do VBLANK stuff, in func to make main code block smaller
 */
func fw_vblank {
		// GFX init
		
		NUSIZ0=NUSIZ1=a=[MSL_SIZE_8 | THREE_COPIES_CLOSE]
		
		// Position missiles and ball
		wsync
		CTRLPF=a=[PF_MIRRORED | PF_PRIO_BALLABOVE | BALL_SIZE_1]	// 5
		COLUPF=a=0x00												// 5
		COLUBK=a													// 3
		COLUP0=COLUP1=a												// 6
		PF2=a PF0=a=0b11110000 PF1=a=0b10000000						// 13
		ENABL=a=[OBJ_DISABLE]										// 5
		ENAM0=ENAM1=a=[OBJ_ENABLE]									// 8
		HMM0=a=[HM_RIGHT_1] 										// 5
		// @50
		RESBL=a
		HMM1=a=[HM_NO_MOTION]
		sl2
		RESM0=a RESM1=a
		HMBL=a=[HM_NO_MOTION] wsync
		// @0
		hmove
		VDELP0=VDELP1=a
		// Position p0 and missiles
		greets_sl12 greets_sl12 
		HMM0=HMM1=a=0
		HMP0=a=[HM_RIGHT_1]
		sl3
		// @49
		RESP0=a
		wsync hmove
		
		// Fade background color
		a=demoState =={
			a=numFrame a>> a>> a?7 >={a=7}
			a<< greets_backcol=a
		} else {
			// Fade out?
			a?127 =={
				a=numFrame a>> a>> a?8 >={a=7 demoState++}
				a^0xff c+ a+7
				a<< greets_backcol=a
			}
		}

		// Move heart (p1)
		a=greets_heart_frame =={
			// New heart position: y
			{greets_rng a&31 a?19}>=
			a?7 <{
				c- a+20
			} else {
				c- a+37
			}
			greets_heart_yPosFix=a
			// x
			{greets_rng a&63 a?39}>=
			a?9 <{
				c- a+145
			} else {
				a?18 <{
					c+ a-8
				} else {
					c+ a-1
				}
			}
			greets_heart_xPos=a
		}
		// Position heart
		greets_heart_yPos=a=greets_heart_yPosFix
		HMP0=a=0
		a=greets_heart_xPos x=1 greets_pos_object wsync hmove
		
		// Color heart
		greets_heart_frame++
		a=greets_heart_frame c+ a-40
		a>> a>> a?15 >={a=15} x=a greets_heart_color=a=greets_heart_fade,x
		a=demoState a?127 >={
			greets_heart_color=a=greets_backcol
		} else {
			a?0 =={
				a=numFrame a?100 <{
					greets_heart_color=a=greets_backcol
				}
			}
		}
		
		HMP1=a=0
}


/* Main
 */
func greetings_main {
	s=x=0xff
		
	// Init
	demoState=a=0 numFrame=a
	greets_heart_frame=a
	greets_backcol=a
	greets_seed=a=2
	greets_heart_yPosFix=a=0xff
	

	// Clear scroller
	scr_curchar=scr_mask=a=0
	// one more because of scr_zero
	x=[6*13] {
		scr_data,x=a
		x--
	}>=0
	<0goto GreetingsStart

	// Kernel
	{
		/**************************************************
		 * Overscan
		 **************************************************/
		
		overscan_start
		
GreetingsStart:

		// timer
		numFrame++ =={ demoState++ }
		a=demoState <0{
			far cube_main
		}
		
		// Do scrolling
		a=demoState a?127 <{
			far do_scroll
			far do_scroll
		}
		
		overscan_end


		/**************************************************
		 * VBlank
		 **************************************************/
		
		vblank_start

		fw_vblank

		far music
		
		vblank_end
				
		/**************************************************
		 * Visible screen
		 **************************************************/
		
		screen_start

        show_head_until_hair		// 0-25
		show_head_hair				// 26-59
		show_head_until_brow		// 60-88
		show_head_brow				// 89-90
		show_head_until_scroller	// 91-135
		show_head_scroller			// 136-163
		show_head_until_chin		// 164-175
		show_head_chin				// 176-185
		show_head_until_hand		// 186-190
		show_head_hand				// 191-213
		
		screen_end
		
	} always
}
