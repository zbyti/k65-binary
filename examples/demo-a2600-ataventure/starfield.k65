



func starfield_core {
	wsync em0=a=0 cp0=a=0x0F gp0=a=0 ns0=a=0 rmp0=a=0


	x=fx_counter
	{
		x?38 >= goto sfc_break
		wsync wsync
		x++
	} always
	sfc_break:

	a=sx1 c- a+49 sx1=a
	x=fx_counter
	!={
		c- a=0
		nocross {
			wsync em0=y=0
			{ a-15 }>=0 a<< a<< a<< a<< a^0x70 hm0=a rm0=a

			wsync
			hmove=a em0=a=2				// show
			a=x a&0x0E =={ a=15 } cp0=a
			c- a=sy0 a-231 sy0=a
			   a=sy1 a-33  sy1=a

			c- a=sx0 a+sy0 sx0=a	// add velo
			   a=sx1 a+sy1 sx1=a

			a=sx1
			a&0x7F
			c- a+12

			x--
		}!=
		wsync em0=a=0
	}

	a=sscroll a^15
	txinit_scroll
	wsync
	a=8 a&?sscroll
	!=   { txline     }
	else { txline_rev }
	pf0=a=0 pf1=a


//	a=subframe a^15 a&15
//	txinit_scroll
//	a=&>iA txcharset
//	a=frame c- a+stxt tmp1=a
//	a=0 a+stxt1 tmp2=a
//	txtc+1=a=&>Col
//	rdline
//	txtc=a=acol+0
//	wsync
//	a=8 a&?subframe
//	!=   { txline     }
//	else { txline_rev }
//	pf0=a=0 pf1=a


	sx0=a=0
	a=subframe a^0xFF sx1=a
	a=subframe a<< a<< a<< a<< a<< sy0=a
	a=frame sy1=a

	gp0=a=0 gp1=a ns0=a=0
	cp0=a=0x0F

	x=fx_counter
	!={
		c- a=0
		nocross {
			wsync em0=y=0
			{ a-15 }>=0 a<< a<< a<< a<< a^0x70 hm0=a rm0=a

			wsync
			hmove=a em0=a=2				// show
			a=x a&0x0E =={ a=15 } cp0=a
			c- a=sy0 a+231 sy0=a
			   a=sy1 a+33  sy1=a

			c- a=sx0 a-sy0 sx0=a	// add velo
			   a=sx1 a-sy1 sx1=a

			a&0x7F
			c- a+12

			x--
		}!=
		wsync em0=a=0
	}
}

func startext_init {
	// input:
	//		X - text low address
	//		Y - text high address
	stxt=x stxt1=y
	a=0 sscroll=a ssfail=a
	y=2
	{
		startexbuff,y=a
		startexbuff+3,y=a
		startexbuff+6,y=a
		startexbuff+9,y=a
		y--
	}>=0
}


inline _starfield_none {
	fx_counter=a=0
	far startext_process
	starfield_core
}

inline _starfield_emerge {
	a=0 a&subframe =={
		a=fx_counter
		a?38 <{ fx_counter++ }
	}
	far startext_process
	starfield_core
}

inline _starfield {
	fx_counter=a=38
	far startext_process
	starfield_core
}


inline _starsync {
	far fsync1 far song_player
	fsync2
	far fsync3
}


func startext_nostars {
	startext_init
	{ _starsync demo_event=a=1 _starfield_none		a=demo_event }== demo_event=a=0
}

func startext_emerge {
	startext_init
	fx_counter=a=0
	{ _starsync _starfield_emerge		a=demo_event }== demo_event=a=0
}

func startext {
	startext_init
	{ _starsync _starfield			a=demo_event }== demo_event=a=0
}
